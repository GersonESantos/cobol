000010*    PEXTENSO.
000020*    ROTINA PARA ESCREVER NUMERO POR EXTENSO.
000030*    AUTOR: SOUTO - MAR/89.
000040*    FAZER COPY "WRKEXTENSO"  NA WORKING STORAGE
000050*    FAZER COPY "PEXTENSO"    NA PROCEDURE DIVISION.
000060*    PASSAR O NUMERO A SER ESCRITO EM NUMERO-EXT  PIC(15)V99.
000070*    FAZER PERFORM 0000-EXTENSO THRU 0009-F-EXTENSO.
000080*    A SAIDA DA ROTINA SERA FEITA NO CAMPO EXTENSO-EXT PIC X(260).
000090*    OBS: O CAMPO NUMERO-EXT SERA ALTERADO PELA ROTINA.
000100 0000-EXTENSO-U.
000110     MOVE "N"        TO CENTAVOS-U.
000120     IF  (CENTENA-EXT-U = ZEROS) AND (RESTO-EXT-U = ZEROS)
000130         MOVE "S"    TO CENTAVOS-U.
000140     MOVE 1                      TO POS-EXT-U.
000150     MOVE ASTER-U                TO EXTENSO-EXT-U.
000160     PERFORM 0010-MILHAR-U       THRU 0019-F-MILHAR-U
000170             VARYING IND-EXT-U FROM 1 BY 1 UNTIL IND-EXT-U > 5.
000180     MOVE CENTAVOS-EXT-U         TO CENTENA-EXT-U.
000190     MOVE ZEROS                  TO CENTAVOS-EXT-U.
000200     PERFORM 0010-MILHAR-U       THRU 0019-F-MILHAR-U.
000210     IF  CENTAVOS-U = "S"
000220         MOVE "D"         TO LETRA-EXT-U(POS-EXT-U)
000230         ADD 1            TO POS-EXT-U
000240         MOVE "E"         TO LETRA-EXT-U(POS-EXT-U)
000250         ADD 1            TO POS-EXT-U
000260         MOVE SPACES      TO LETRA-EXT-U(POS-EXT-U)
000270         ADD 1            TO POS-EXT-U
000280         MOVE 5           TO IND-EXT-U
000290         MOVE 1           TO FLAG-EXT-U
000300         MOVE SING-EXT-U(5) TO DESCR-EXT-U
000310         PERFORM 0030-INSERE-TEXTO-U THRU 0039-F-INSERE-TEXTO-U
000320                 UNTIL FLAG-EXT-U > LSING-EXT-U(5).
000330 0009-F-EXTENSO-U.
000340     EXIT.
000350 0010-MILHAR-U.
000360     IF  (EXTENSO-EXT-U NOT = ASTER-U)
000370         IF  (CENTENA-EXT-U > ZEROS)
000380             IF  (RESTO-EXT-U = ZEROS) AND (CENTAVOS-EXT-U = 0)
000390                 MOVE "E"         TO LETRA-EXT-U(POS-EXT-U)
000400                 ADD 1            TO POS-EXT-U
000410                 MOVE SPACES      TO LETRA-EXT-U(POS-EXT-U)
000420                 ADD 1            TO POS-EXT-U
000430             ELSE
000440                 SUBTRACT 1       FROM POS-EXT-U
000450                 MOVE ","         TO LETRA-EXT-U(POS-EXT-U)
000460                 ADD 1            TO POS-EXT-U
000470                 MOVE SPACES      TO LETRA-EXT-U(POS-EXT-U)
000480                 ADD 1            TO POS-EXT-U
000490         ELSE
000500             IF  (RESTO-EXT-U = ZEROS) AND (IND-EXT-U < 5)
000510                 MOVE "D"         TO LETRA-EXT-U(POS-EXT-U)
000520                 ADD 1            TO POS-EXT-U
000530                 MOVE "E"         TO LETRA-EXT-U(POS-EXT-U)
000540                 ADD 1            TO POS-EXT-U
000550                 MOVE SPACES      TO LETRA-EXT-U(POS-EXT-U)
000560                 ADD 1            TO POS-EXT-U
000570                 MOVE 5           TO IND-EXT-U.
000580     IF  (CENTENA-EXT-U NOT = ZEROS) OR
000590         ((EXTENSO-EXT-U NOT = ASTER-U) AND (IND-EXT-U = 5))
000600         PERFORM 0020-CENTENA-U     THRU 0029-F-CENTENA-U
000610         IF  CENTENA-EXT-U = 1
000620            MOVE 1                   TO FLAG-EXT-U
000630            MOVE SING-EXT-U(IND-EXT-U)   TO DESCR-EXT-U
000640            PERFORM 0030-INSERE-TEXTO-U THRU 0039-F-INSERE-TEXTO-U
000650                    UNTIL FLAG-EXT-U > LSING-EXT-U(IND-EXT-U)
000660         ELSE
000670            MOVE 1                   TO FLAG-EXT-U
000680            MOVE PLURAL-EXT-U(IND-EXT-U) TO DESCR-EXT-U
000690            PERFORM 0030-INSERE-TEXTO-U THRU 0039-F-INSERE-TEXTO-U
000700                    UNTIL FLAG-EXT-U > LPLURAL-EXT-U(IND-EXT-U).
000710     MOVE RESTO-EXT-U               TO NUM-R-EXT-U.
000720     MOVE ZEROS                   TO FLAG-EXT-U ZEROS-EXT-U.
000730     MOVE SPACES                  TO DESCR-EXT-U.
000740 0019-F-MILHAR-U.
000750     EXIT.
000760 0020-CENTENA-U.
000770     IF  CENTENA-EXT-U = 100
000780         MOVE 1         TO FLAG-EXT-U
000790         MOVE NCEM-EXT-U  TO DESCR-EXT-U
000800         PERFORM 0030-INSERE-TEXTO-U THRU 0039-F-INSERE-TEXTO-U
000810                 UNTIL FLAG-EXT-U > 4
000820         GO TO 0029-F-CENTENA-U.
000830     IF CEM-EXT-U > ZEROS
000840         MOVE 1                 TO FLAG-EXT-U
000850         MOVE CENT-EXT-U(CEM-EXT-U) TO DESCR-EXT-U
000860         PERFORM 0030-INSERE-TEXTO-U THRU 0039-F-INSERE-TEXTO-U
000870                 UNTIL FLAG-EXT-U > LCENT-EXT-U(CEM-EXT-U).
000880     IF  CEM-EXT-U > ZEROS
000890         IF (UNI-EXT-U > ZEROS) OR (DEZ-EXT-U > ZEROS)
000900             MOVE "E"            TO LETRA-EXT-U (POS-EXT-U)
000910             ADD 1               TO POS-EXT-U
000920             MOVE SPACES         TO LETRA-EXT-U (POS-EXT-U)
000930             ADD 1               TO POS-EXT-U.
000940     IF  DEZ-EXT-U NOT > 1
000950         MOVE DEZ-EXT-U TO UNID-EXT-U
000960         MULTIPLY 10  BY UNID-EXT-U
000970         ADD UNI-EXT-U  TO UNID-EXT-U
000980     ELSE
000990         MOVE UNI-EXT-U  TO UNID-EXT-U
001000         MOVE 1                   TO FLAG-EXT-U
001010         MOVE DEZENA-EXT-U(DEZ-EXT-U) TO DESCR-EXT-U
001020         PERFORM 0030-INSERE-TEXTO-U THRU 0039-F-INSERE-TEXTO-U
001030                 UNTIL FLAG-EXT-U > LDEZENA-EXT-U(DEZ-EXT-U)
001040         IF UNI-EXT-U > ZEROS
001050             MOVE "E"            TO LETRA-EXT-U(POS-EXT-U)
001060             ADD 1               TO POS-EXT-U
001070             MOVE SPACES         TO LETRA-EXT-U(POS-EXT-U)
001080             ADD 1               TO POS-EXT-U.
001090     IF  UNID-EXT-U > ZEROS
001100         MOVE 1                     TO FLAG-EXT-U
001110         MOVE UNIDADE-EXT-U(UNID-EXT-U) TO DESCR-EXT-U
001120         PERFORM 0030-INSERE-TEXTO-U THRU 0039-F-INSERE-TEXTO-U
001130                 UNTIL FLAG-EXT-U > LUNIDADE-EXT-U(UNID-EXT-U).
001140 0029-F-CENTENA-U.
001150     EXIT.
001160 0030-INSERE-TEXTO-U.
001170     MOVE LDESCR-EXT-U(FLAG-EXT-U) TO LETRA-EXT-U(POS-EXT-U).
001180     ADD 1 TO POS-EXT-U FLAG-EXT-U.
001190 0039-F-INSERE-TEXTO-U.
001200     EXIT.
