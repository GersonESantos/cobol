000010*    PEXTENSO.
000020*    ROTINA PARA ESCREVER NUMERO POR EXTENSO.
000030*    AUTOR: SOUTO - MAR/89.
000040*    FAZER COPY "WRKEXTENSO"  NA WORKING STORAGE
000050*    FAZER COPY "PEXTENSO"    NA PROCEDURE DIVISION.
000060*    PASSAR O NUMERO A SER ESCRITO EM NUMERO-EXT  PIC(15)V99.
000070*    FAZER PERFORM 0000-EXTENSO THRU 0009-F-EXTENSO.
000080*    A SAIDA DA ROTINA SERA FEITA NO CAMPO EXTENSO-EXT PIC X(260).
000090*    OBS: O CAMPO NUMERO-EXT SERA ALTERADO PELA ROTINA.
000100 0000-EXTENSO.
000110     MOVE "N"        TO CENTAVOS.
000120     IF  (CENTENA-EXT = ZEROS) AND (RESTO-EXT = ZEROS)
000130         MOVE "S"    TO CENTAVOS.
000140     MOVE 1                      TO POS-EXT.
000150     MOVE ASTER                  TO EXTENSO-EXT.
000160     PERFORM 0010-MILHAR         THRU 0019-F-MILHAR
000170             VARYING IND-EXT FROM 1 BY 1 UNTIL IND-EXT > 5.
000180     MOVE CENTAVOS-EXT           TO CENTENA-EXT.
000190     MOVE ZEROS                  TO CENTAVOS-EXT.
000200     PERFORM 0010-MILHAR         THRU 0019-F-MILHAR.
000210     IF  CENTAVOS = "S"
000220         MOVE "D"         TO LETRA-EXT(POS-EXT)
000230         ADD 1            TO POS-EXT
000240         MOVE "E"         TO LETRA-EXT(POS-EXT)
000250         ADD 1            TO POS-EXT
000260         MOVE SPACES      TO LETRA-EXT(POS-EXT)
000270         ADD 1            TO POS-EXT
000280         MOVE 5           TO IND-EXT
000290         MOVE 1           TO FLAG-EXT
000300         MOVE SING-EXT(5) TO DESCR-EXT
000310         PERFORM 0030-INSERE-TEXTO THRU 0039-F-INSERE-TEXTO
000320                 UNTIL FLAG-EXT > LSING-EXT(5).
000330 0009-F-EXTENSO.
000340     EXIT.
000350 0010-MILHAR.
000360     IF  (EXTENSO-EXT NOT = ASTER)
000370         IF  (CENTENA-EXT > ZEROS)
000380             IF  (RESTO-EXT = ZEROS) AND (CENTAVOS-EXT = ZEROS)
000390                 MOVE "E"         TO LETRA-EXT(POS-EXT)
000400                 ADD 1            TO POS-EXT
000410                 MOVE SPACES      TO LETRA-EXT(POS-EXT)
000420                 ADD 1            TO POS-EXT
000430             ELSE
000440                 SUBTRACT 1       FROM POS-EXT
000450                 MOVE ","         TO LETRA-EXT(POS-EXT)
000460                 ADD 1            TO POS-EXT
000470                 MOVE SPACES      TO LETRA-EXT(POS-EXT)
000480                 ADD 1            TO POS-EXT
000490         ELSE
000500             IF  (RESTO-EXT = ZEROS) AND (IND-EXT < 5)
000510                 MOVE "D"         TO LETRA-EXT(POS-EXT)
000520                 ADD 1            TO POS-EXT
000530                 MOVE "E"         TO LETRA-EXT(POS-EXT)
000540                 ADD 1            TO POS-EXT
000550                 MOVE SPACES      TO LETRA-EXT(POS-EXT)
000560                 ADD 1            TO POS-EXT
000570                 MOVE 5           TO IND-EXT.
000580     IF  (CENTENA-EXT NOT = ZEROS) OR
000590         ((EXTENSO-EXT NOT = ASTER) AND (IND-EXT = 5))
000600         PERFORM 0020-CENTENA     THRU 0029-F-CENTENA
000610         IF  CENTENA-EXT = 1
000620             MOVE 1                   TO FLAG-EXT
000630             MOVE SING-EXT(IND-EXT)   TO DESCR-EXT
000640             PERFORM 0030-INSERE-TEXTO THRU 0039-F-INSERE-TEXTO
000650                     UNTIL FLAG-EXT > LSING-EXT(IND-EXT)
000660         ELSE
000670             MOVE 1                   TO FLAG-EXT
000680             MOVE PLURAL-EXT(IND-EXT) TO DESCR-EXT
000690             PERFORM 0030-INSERE-TEXTO THRU 0039-F-INSERE-TEXTO
000700                     UNTIL FLAG-EXT > LPLURAL-EXT(IND-EXT).
000710     MOVE RESTO-EXT               TO NUM-R-EXT.
000720     MOVE ZEROS                   TO FLAG-EXT ZEROS-EXT.
000730     MOVE SPACES                  TO DESCR-EXT.
000740 0019-F-MILHAR.
000750     EXIT.
000760 0020-CENTENA.
000770     IF  CENTENA-EXT = 100
000780         MOVE 1         TO FLAG-EXT
000790         MOVE NCEM-EXT  TO DESCR-EXT
000800         PERFORM 0030-INSERE-TEXTO THRU 0039-F-INSERE-TEXTO
000810                 UNTIL FLAG-EXT > 4
000820         GO TO 0029-F-CENTENA.
000830     IF CEM-EXT > ZEROS
000840         MOVE 1                 TO FLAG-EXT
000850         MOVE CENT-EXT(CEM-EXT) TO DESCR-EXT
000860         PERFORM 0030-INSERE-TEXTO THRU 0039-F-INSERE-TEXTO
000870                 UNTIL FLAG-EXT > LCENT-EXT(CEM-EXT).
000880     IF  CEM-EXT > ZEROS
000890         IF (UNI-EXT > ZEROS) OR (DEZ-EXT > ZEROS)
000900             MOVE "E"            TO LETRA-EXT (POS-EXT)
000910             ADD 1               TO POS-EXT
000920             MOVE SPACES         TO LETRA-EXT (POS-EXT)
000930             ADD 1               TO POS-EXT.
000940     IF  DEZ-EXT NOT > 1
000950         MOVE DEZ-EXT TO UNID-EXT
000960         MULTIPLY 10  BY UNID-EXT
000970         ADD UNI-EXT  TO UNID-EXT
000980     ELSE
000990         MOVE UNI-EXT  TO UNID-EXT
001000         MOVE 1                   TO FLAG-EXT
001010         MOVE DEZENA-EXT(DEZ-EXT) TO DESCR-EXT
001020         PERFORM 0030-INSERE-TEXTO THRU 0039-F-INSERE-TEXTO
001030                 UNTIL FLAG-EXT > LDEZENA-EXT(DEZ-EXT)
001040         IF UNI-EXT > ZEROS
001050             MOVE "E"            TO LETRA-EXT(POS-EXT)
001060             ADD 1               TO POS-EXT
001070             MOVE SPACES         TO LETRA-EXT(POS-EXT)
001080             ADD 1               TO POS-EXT.
001090     IF  UNID-EXT > ZEROS
001100         MOVE 1                     TO FLAG-EXT
001110         MOVE UNIDADE-EXT(UNID-EXT) TO DESCR-EXT
001120         PERFORM 0030-INSERE-TEXTO THRU 0039-F-INSERE-TEXTO
001130                 UNTIL FLAG-EXT > LUNIDADE-EXT(UNID-EXT).
001140 0029-F-CENTENA.
001150     EXIT.
001160 0030-INSERE-TEXTO.
001170     MOVE LDESCR-EXT(FLAG-EXT) TO LETRA-EXT(POS-EXT).
001180     ADD 1 TO POS-EXT FLAG-EXT.
001190 0039-F-INSERE-TEXTO.
001200     EXIT.
